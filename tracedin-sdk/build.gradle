import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

buildscript {
    ext {
        protobufVersion = '3.21.5'
        protobufPluginVersion = '0.9.4'
        grpcVersion = '1.65.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.vanniktech.maven.publish' version '0.29.0'
    id 'signing'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group = 'io.github.tracedin'
version = '0.0.4'

def artifactName = 'tracedin-sdk'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}
// https://stackoverflow.com/questions/78925084/spring-boot-starter-opentelemetry-getting-clasnotfound-for-doublegauge
def addOpenTelemetryInstrumentation(String module, String version) {
    dependencies {
        implementation("io.opentelemetry.instrumentation:$module:$version") {
            exclude group: 'io.opentelemetry', module: 'opentelemetry-api'
            exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk'
            exclude group: 'io.opentelemetry', module: 'opentelemetry-api-incubator'
        }
    }
}

dependencies {
    implementation 'io.opentelemetry:opentelemetry-sdk:1.37.0'
    implementation 'io.opentelemetry:opentelemetry-api:1.37.0'
    implementation 'io.opentelemetry:opentelemetry-api-incubator:1.37.0-alpha'

    addOpenTelemetryInstrumentation('opentelemetry-jdbc','2.3.0-alpha')
    addOpenTelemetryInstrumentation('opentelemetry-kafka-clients-2.6','1.33.6-alpha')

    implementation 'org.springframework.boot:spring-boot-starter-tomcat:3.3.3'
    implementation 'org.springframework.boot:spring-boot-starter-json:3.3.3'
    implementation 'org.springframework.boot:spring-boot-starter-aop:3.4.0'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    // grpc 서버, 클라이언트 설정
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
    // Spring Boot와 gRPC의 통합을 간편하게 도와주는 스타터
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    // Netty Shaded 사용(gRPC 서버와 클라이언트의 Netty 전송 계층을 제공)
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"     // Protobuf 메시지와 gRPC의 통합을 지원
    implementation "io.grpc:grpc-stub:${grpcVersion}"         // gRPC 클라이언트 스텁을 생성
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    // 이걸 추가해야 gRPC 컴파일시 javax 어노테이션 오류가 발생하지 않는다.


    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.3'

    // https://mvnrepository.com/artifact/org.apache.kafka/kafka-clients
    implementation 'org.apache.kafka:kafka-clients:3.7.0'
    // https://mvnrepository.com/artifact/org.springframework.kafka/spring-kafka
    implementation 'org.springframework.kafka:spring-kafka:3.2.4'

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation 'com.zaxxer:HikariCP:5.1.0'
}

protobuf {
    // Protobuf 컴파일러를 지정하여 .proto 파일을 컴파일합니다.
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    // 생성된 파일을 정리합니다.
    clean {
        delete generatedFilesBaseDir
    }
    // gRPC 플러그인을 설정하여 Protobuf 파일로부터 gRPC 관련 코드를 생성합니다.
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    // 모든 프로토콜 버퍼 작업에 대해 gRPC 플러그인을 적용합니다.
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Empty(), true))
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)
    coordinates(group, artifactName, version)

    pom {
        name = artifactName
        description = 'Tracedin Application SDK for Java'
        url = 'https://tracedin.github.io'

        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/license/MIT'
            }
        }

        developers {
            developer {
                name = 'Jiwon KKang'
                email = 'rkdwldnjs878@gmail.com'
                organization = 'Chungbuk National Univ'
                organizationUrl = 'https://github.com/JiwonKKang'

            }
            developer {
                name = 'Sukkyun Hong'
                email = 'goobghd@gmail.com'
                organization = 'Chungbuk National Univ'
                organizationUrl = 'https://github.com/sukkyun2'
            }
        }

        scm {
            connection = 'scm:git:github.com/tracedin/tracein-sdk.git'
            developerConnection = 'scm:git:ssh://github.com/tracedin/tracein-sdk'
            url = 'https://github.com/tracedin/tracein-sdk/tree/main'
        }
    }
}
